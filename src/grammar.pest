Program = _{ SOI ~ ((Embed | Action | IntroRetro) ~ Newline?)* ~ EOI }

Action = { ActionName ~ (":" ~ (Iota | BookkeeperValue | ActionDiscriminator))? }

ActionName = {'A'..'Z' ~ (ASCII_ALPHA | "+" | "-" | "'")+}

BookkeeperValue = {("v" | "-")+}

ActionDiscriminator = { ('A'..'Z') ~ ('A'..'z' | "-")* ~ ('A'..'Z')? }

Iota = { Number | Vector | Entity | Influence | Bool | Pattern}

Vector = {"(" ~ Number ~ "," ~ Number ~ "," ~ Number ~ ")" }

Number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?  }

Entity = {"{" ~ String ~ "," ~ EntityType ~ ("," ~ Uuid)? ~ "}" }

Uuid = ${ UuidStart ~ "-" ~ UuidSlice ~ "-" ~ UuidSlice ~ "-" ~ UuidSlice ~ "-" ~ UUIDEnd }

UuidStart = @{ (ASCII_DIGIT | 'a'..'z'){8} }

UuidSlice = @{ (ASCII_DIGIT | 'a'..'z'){4} }

UUIDEnd = @{ (ASCII_DIGIT | 'a'..'z'){12} }

EntityType = @{'a'..'z'+ ~ ":" ~ 'a'..'z'+}

Influence = @{"Null" | "Garbage"}

Bool = @{"True" | "False"}

Pattern = ${ PatternName | ActionName }

String = @{("\"" ~ ("\\\"" | (!"\"" ~ ANY))+ ~ "\"")}

PatternName = ${ PatternDirection ~ " " ~ PatternSignature}

PatternDirection = @{("NORTHEAST" | "EAST" | "SOUTHEAST" | "SOUTHWEST" | "WEST" | "NORTHWEST")}

PatternSignature = @{("q" | "a" | "w" | "e" | "d")+}

IntroRetro = { "{" | "}" }

Embed = ${ "<" ~ ( IntroEmbed | ConsiderEmbed | SmartEmbed | DirectEmbed ) ~ ">" }

DirectEmbed = @{ Iota }

IntroEmbed = @{ "{" ~ Iota ~ "}" }

ConsiderEmbed = @{"\\" ~ Iota }

SmartEmbed = @{"<" ~ Iota ~ ">"}

WHITESPACE = _{ " " | "\t" }

Newline = _{ ("\r\n" | "\n") }

