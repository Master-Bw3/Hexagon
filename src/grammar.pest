File = _{ SOI ~ Hex ~ EOI }

Hex = _{ ((EscapedIntroRetro | IfBlock | Embed | Op | Var | Action | Term ) ~ Newline? | Newline)* }

IfBlock = {If ~ Then ~ (ElseIf ~ Then ~ Newline? )* ~ Else?}

If =  {"if" ~ Newline? ~ Term}

Then = {"then" ~ Newline? ~ Term}

Else = {"else" ~ Newline? ~ Term}

ElseIf = {"else if" ~ Newline? ~ Term}

Term = {"{" ~ Hex ~ "}"}

Action = { ActionName ~ (":" ~ EntityType)? ~ (":" ~ WHITESPACE* ~ ( BookkeeperValue | Iota | IntroRetro ))? }

ActionName = !{ASCII_ALPHA_UPPER ~ (ASCII_ALPHA | "+" | "-" | "'")+}

BookkeeperValue = @{("v" | "-")+ ~ !(ASCII_DIGIT)}

// ActionDiscriminator = { (ASCII_ALPHA_UPPER) ~ ('A'..'z' | "-")* ~ (ASCII_ALPHA_UPPER)? }

Iota = { Number | Vector | Entity | Influence | Bool | Pattern | List}

List = !{"[" ~ ((Iota ~ ",")* ~ Iota)? ~ "]" }

Vector = {"(" ~ Number ~  "," ~ WHITESPACE? ~ Number ~ "," ~ WHITESPACE? ~ Number ~ ")" }

Number = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?  }

Entity = {"@" ~ (ASCII_ALPHA | ASCII_DIGIT | "_")+ }

Uuid = ${ UuidStart ~ "-" ~ UuidSlice ~ "-" ~ UuidSlice ~ "-" ~ UuidSlice ~ "-" ~ UUIDEnd }

UuidStart = @{ (ASCII_DIGIT | ASCII_ALPHA_LOWER){8} }

UuidSlice = @{ (ASCII_DIGIT | ASCII_ALPHA_LOWER){4} }

UUIDEnd = @{ (ASCII_DIGIT | ASCII_ALPHA_LOWER){12} }

EntityType = @{"Non-"? ~ ("Animal" | "Monster" | "Living" | "Item" | "Player" | "Misc")}

Influence = @{("Null" | "Garbage") ~ !(WHITESPACE? ~ ASCII_ALPHA)}

Bool = @{("True" | "False") ~ !(WHITESPACE? ~ ASCII_ALPHA)}

Pattern = ${ IntroRetro | PatternName | Action }

String = @{("\"" ~ ("\\\"" | (!"\"" ~ ANY))+ ~ "\"")}

PatternName = ${ PatternDirection ~ " " ~ PatternSignature}

PatternDirection = @{("NORTHEAST" | "EAST" | "SOUTHEAST" | "SOUTHWEST" | "WEST" | "NORTHWEST")}

PatternSignature = @{("q" | "a" | "w" | "e" | "d")+}

EscapedIntroRetro = {"Consideration" ~ Newline? ~ IntroRetro}

IntroRetro = { "{" | "}" }

Embed = ${ "<" ~ ( IntroEmbed | ConsiderEmbed | SmartEmbed | DirectEmbed ) ~ ">" }

DirectEmbed = ${ Iota }

IntroEmbed = ${ "{" ~ Iota ~ "}" }

ConsiderEmbed = ${"\\" ~ Iota }

SmartEmbed = ${"<" ~ Iota ~ ">"}

Var = ${"$" ~ ASCII_ALPHANUMERIC+}

Op = ${OpName ~ "(" ~ (Iota | Var)? ~ ")"}

OpName =  {ASCII_ALPHA_UPPER ~ ASCII_ALPHA_LOWER*}

WHITESPACE = _{ " " | "\t" }

COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!Newline ~ ANY)*) }

Newline = _{ ("\r\n" | "\n") }

