File = _{ SOI ~ Hex ~ EOI }

Hex = _{ ((IfBlock | Embed | Op | Var | Action | IntroRetro) ~ Newline? | Newline)* }

IfBlock = {If ~ Then ~ (ElseIf ~ Then)* ~ Else?}

If =  {"if" ~ Term}

Then = {"then"  ~ Term}

Else = {"else" ~ Term}

ElseIf = {"else if" ~ Term}

Term = {"(" ~ Hex ~ ")"}

Action = { ActionName ~ (":" ~ (Iota | BookkeeperValue | ActionDiscriminator))? }

ActionName = !{ASCII_ALPHA_UPPER ~ (ASCII_ALPHA | "+" | "-" | "'")+}

BookkeeperValue = {("v" | "-")+}

ActionDiscriminator = { (ASCII_ALPHA_UPPER) ~ ('A'..'z' | "-")* ~ (ASCII_ALPHA_UPPER)? }

Iota = { Number | Vector | Entity | Influence | Bool | Pattern | List}

List = !{"[" ~ ((Iota ~ ",")* ~ Iota)? ~ "]" }


Vector = {"(" ~ Number ~ "," ~ Number ~ "," ~ Number ~ ")" }

Number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?  }

Entity = {"{" ~ String ~ "," ~ EntityType ~ ("," ~ Uuid)? ~ "}" }

Uuid = ${ UuidStart ~ "-" ~ UuidSlice ~ "-" ~ UuidSlice ~ "-" ~ UuidSlice ~ "-" ~ UUIDEnd }

UuidStart = @{ (ASCII_DIGIT | ASCII_ALPHA_LOWER){8} }

UuidSlice = @{ (ASCII_DIGIT | ASCII_ALPHA_LOWER){4} }

UUIDEnd = @{ (ASCII_DIGIT | ASCII_ALPHA_LOWER){12} }

EntityType = @{ASCII_ALPHA_LOWER+ ~ ":" ~ ASCII_ALPHA_LOWER+}

Influence = @{"Null" | "Garbage"}

Bool = @{"True" | "False"}

Pattern = ${ PatternName | ActionName }

String = @{("\"" ~ ("\\\"" | (!"\"" ~ ANY))+ ~ "\"")}

PatternName = ${ PatternDirection ~ " " ~ PatternSignature}

PatternDirection = @{("NORTHEAST" | "EAST" | "SOUTHEAST" | "SOUTHWEST" | "WEST" | "NORTHWEST")}

PatternSignature = @{("q" | "a" | "w" | "e" | "d")+}

IntroRetro = { "{" | "}" }

Embed = ${ "<" ~ ( IntroEmbed | ConsiderEmbed | SmartEmbed | DirectEmbed ) ~ ">" }

DirectEmbed = ${ Iota }

IntroEmbed = ${ "{" ~ Iota ~ "}" }

ConsiderEmbed = ${"\\" ~ Iota }

SmartEmbed = ${"<" ~ Iota ~ ">"}

Var = ${"$" ~ ASCII_ALPHANUMERIC+}

Op = ${OpName ~ "(" ~ (Iota | Var)? ~ ")"}

OpName =  {ASCII_ALPHA_UPPER ~ ASCII_ALPHA_LOWER*}

WHITESPACE = _{ " " | "\t" }

Newline = _{ ("\r\n" | "\n") }

